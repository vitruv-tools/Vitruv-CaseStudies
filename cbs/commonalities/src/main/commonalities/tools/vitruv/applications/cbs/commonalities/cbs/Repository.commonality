import tools.vitruv.applications.cbs.operators.domaincommon.operators._

import "http://palladiosimulator.org/PalladioComponentModel/5.2" as pcm

concept ComponentBasedSystems

commonality Repository {

	with pcm:(Repository in Resource)
	whereat {
		-> Resource.path = "model/",
		Resource.fileExtension = "repository"
	}

	with ObjectOrientedDesign:(
		Package as RepositoryPackage,
		Package as ContractsPackage,
		Package as DatatypesPackage
	)
	whereat {
		ContractsPackage in RepositoryPackage,
		ContractsPackage.name = "contracts",

		DatatypesPackage in RepositoryPackage,
		DatatypesPackage.name = "datatypes"
	}

	has name {
		= pcm:Repository.entityName
		-> pcm:Resource.name

		= firstUpper(ObjectOrientedDesign:RepositoryPackage.name)
	}

	has components referencing ComponentBasedSystems:Component {
		= pcm:Repository.components__Repository

		// Note: We assume that the contracts and datatypes packages do not contain classes which match the Component
		// participation. Otherwise these packages would need to be ignored here.
		= ObjectOrientedDesign:RepositoryPackage.subPackages
	}

	has componentInterfaces referencing ComponentBasedSystems:ComponentInterface {
		= pcm:Repository.interfaces__Repository

		= ObjectOrientedDesign:ContractsPackage.interfaces
	}

	has compositeDataTypes referencing ComponentBasedSystems:CompositeDataType {
		= pcm:Repository.dataTypes__Repository

		= ObjectOrientedDesign:DatatypesPackage.classes
	}
}
