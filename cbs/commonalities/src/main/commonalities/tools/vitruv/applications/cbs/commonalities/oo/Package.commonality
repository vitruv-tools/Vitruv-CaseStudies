import tools.vitruv.applications.cbs.commonalities.java.operators._

import "http://www.eclipse.org/uml2/5.0.0/UML" as uml
import "http://www.emftext.org/java" as java

concept ObjectOrientedDesign

// Optionally externally contained by ObjectOrientedDesign:Package.subPackages
commonality Package {

	// Optionally externally contained by uml:Package.packagedElement
	with uml:(Package, single Model, Resource)
	whereat {
		Package in Model,
		Model in Resource,
		// TODO Use a condition operator to ask the user for the model name and path
		-> Model.name = "model",
		-> Resource.path = "model/",
		-> Resource.name = "model",
		Resource.fileExtension = "uml"
	}

	// Optionally externally contained by java:Package.javaSubPackages
	with java:(Package in Resource)
	whereat {
		-> Resource.path = "src/",
		Resource.name = "package-info",
		Resource.fileExtension = "java"
	}

	has name {
		= uml:Package.name

		= java:Package.name
		-> javaPackageResourcePathFromName(java:Resource.path, "src/", java:Package)
	}

	// Note: Uml package hierarchy is mapped to containment hierarchy of OO:Package, which is then mapped to
	// java:Package.namespaces.
	// Note: When the java:Package.namespaces changes for an existing Java package, we assume that this change happens
	// consistently with an update to the resource in which the Java package is contained. We therefore do not have to
	// update the Resource.path in this situation.
	has subPackages referencing ObjectOrientedDesign:Package {
		// Note: Any elements that are not of type uml:Package are implicitly ignored.
		= uml:Package.packagedElement

		// Note: When inserting a package, the operator also updates the path of the corresponding resource.
		// TODO Explicitly pass the referenced java:Resource? Currently the operator retrieves it from the correspondence model.
		// TODO Allow the operator to update the resource path after the name or namespaces have changed, but the attribute reference still holds?
		// -> might better fit an attribute mapping
		= java:Package.javaSubPackages(ref Package.namespaces, Package.namespaces, Package.name) 
	}

	has classes referencing ObjectOrientedDesign:Class {
		// Note: Any elements that are not of type uml:Class are implicitly ignored.
		= uml:Package.packagedElement
		// Note: Any compilation units that don't correspond to an ObjectOrientedDesign:Class are implicitly ignored.
		= java:Package.javaPackageCompilationUnits(ref CompilationUnit.namespaces, Package.namespaces, Package.name)
	}

	has interfaces referencing ObjectOrientedDesign:Interface {
		// Note: Any elements that are not of type uml:Interface are implicitly ignored.
		= uml:Package.packagedElement
		// Note: Any compilation units that don't correspond to an ObjectOrientedDesign:Interface are implicitly ignored.
		= java:Package.javaPackageCompilationUnits(ref CompilationUnit.namespaces, Package.namespaces, Package.name) 
	}
}
